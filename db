#!perl

use 5.010; use strict; use warnings;
use Config::Auto;
use File::Spec;
use Getopt::Long;

eval { require Term::ANSIColor };
my $HAS_COLOR = $@ ? 0 : 1;
if($HAS_COLOR && $^O eq 'MSWin32') {
    eval { require Win32::Console::ANSI };
    $HAS_COLOR = $@ ? 0 : 1;
}
if($HAS_COLOR) {
    Term::ANSIColor->import('color');
}
else {
    *color = sub { '' };        # make stub when color is not available
}

my $config_obj = Config::Auto->new(source => 'do_sql.ini');
my $config = $config_obj->parse();
GetOptions(\my %database,
    'dump|d'    => \my $dump,
    'verbose|v' => \my $verbose,
    'help'      => sub { help() },
    sort keys %$config
) or help();

my $db  = (keys %database)[0];
$db = shift unless defined $db;
my $cfg = $config->{$db};

help('Valid database has to be specified')
    unless defined $cfg;

# mysql only
if($cfg->{driver}) {
    help($cfg->{driver}." is not supported, mysql only");
}

# construct mysql command-line
my $exe = $dump ? 'mysqldump' : 'mysql';
my @runargs = (
    $exe,
    "-h".$cfg->{serv},      # host
    "-u".$cfg->{user},      # username
    ($cfg->{pass} ? "-p".$cfg->{pass} : ()),    # password if defined
    ($cfg->{db}   ? "-D".$cfg->{db}   : ()),    # database if defined
    @ARGV
);
warn "Called: @runargs\n" if $verbose;

# intro message
unless($dump) {
    my $msg = sprintf "Connecting to %s server as %s account",$cfg->{serv},$cfg->{user};
    print color('bold yellow');
    print "="x length($msg),"\n";
    print $msg,"\n";
    print "$db: ",$cfg->{note}||'',"\n";
    print "="x length($msg),"\n";
    print color('reset');
}

# run mysql
system @runargs;

sub help {
    warn "Error: @_\n\n" if @_;

    my $database_list = '';
    for my $db (sort keys %$config) {
        $database_list .=
            sprintf "    %-15s %s\n", $db, $config->{$db}{note} || '';
    }
    my $config_file = File::Spec->rel2abs($config_obj->file);

die <<USAGE;
Usage: db [-d] [-v] server [additional params for mysql/mysqldump]

    --help          print usage message and exit}
    -d, --dump      run mysqldump instead of console
    -v, --verbose   print command-line

Databases available:

    Edit "$config_file" file to add databases.

$database_list
The database can be specified also as an option for backwards compatibility

  db localhost                        connect to localhost configuration
  db -v localhost                     connect and print command-line
  db -d localhost database >out.sql   dump "database" from localhost config to "out.sql" file
USAGE
}
